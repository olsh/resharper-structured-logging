plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'org.jetbrains.intellij' version '0.7.2' apply false
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
buildDir = 'gradle-build'
version = ext.PluginVersion

// Workaround this issue https://github.com/JetBrains/gradle-intellij-plugin/issues/630
apply plugin: 'org.jetbrains.intellij'

compileKotlin {
    kotlinOptions { jvmTarget = "1.8" }
}

intellij {
    type = 'RD'
    version = "${ProductVersion}"
    downloadSources = false
    instrumentCode = false
}

sourceSets {
    main {
        java.srcDir 'src/rider/main/kotlin'
        resources.srcDir 'src/rider/main/resources'
    }
}

repositories {
    maven { url 'https://cache-redirector.jetbrains.com/intellij-repository/snapshots' }
    maven { url 'https://cache-redirector.jetbrains.com/maven-central' }
}

prepareSandbox {
    def dotNetFiles = [
            "${DotNetOutputDirectory}/${DotNetProjectName}.dll",
            "${DotNetOutputDirectory}/${DotNetProjectName}.pdb",
    ]

    dotNetFiles.forEach({ f ->
        def file = file(f)
        from(file, { into "${intellij.pluginName}/dotnet" })
    })

    doLast {
        dotNetFiles.forEach({ f ->
            def file = file(f)
            if (!file.exists()) throw new RuntimeException("File ${file} does not exist")
        })
    }
}

wrapper {
    gradleVersion = '6.1.1'
    distributionType = Wrapper.DistributionType.ALL
    distributionUrl = "https://cache-redirector.jetbrains.com/services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
